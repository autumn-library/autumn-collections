#Использовать annotations
#Использовать collectionos
#Использовать reflector

Перем ПрилепляемыеКоллекции;
Перем ИнтерфейсПрилепляемойКоллекции;
Перем Рефлектор;

Процедура ПриСозданииОбъекта()

	ПрилепляемыеКоллекции = Новый КартаСоответствие();

	ИнтерфейсПрилепляемойКоллекции = Новый ИнтерфейсОбъекта()
		.ПроцедураИнтерфейса("Добавить", 2)
		.ФункцияИнтерфейса("Получить", 0);

	Рефлектор = Новый Рефлектор();

КонецПроцедуры

Процедура ДобавитьПрилепляемуюКоллекцию(Тип) Экспорт

	РефлекторОбъекта = Новый РефлекторОбъекта(Тип);

	Методы = РефлекторОбъекта.ПолучитьТаблицуМетодов("ПрилепляемаяКоллекция", Ложь);

	Аннотация = РаботаСАннотациями.ПолучитьАннотацию(
		Методы[0],
		"ПрилепляемаяКоллекция"
	);

	Имя = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация);

	Если ЕстьПрилепляемаяКоллекция(Имя) Тогда
		ВызватьИсключение СтрШаблон("Прилепляемая коллекция %1 уже зарегистрирована", Имя);
	КонецЕсли;

	РефлекторОбъекта.РеализуетИнтерфейс(
		ИнтерфейсПрилепляемойКоллекции,
		Истина
	);

	ПрилепляемыеКоллекции.Вставить(Имя, Тип);

КонецПроцедуры

Функция ПолучитьПрилепляемуюКоллекцию(Имя) Экспорт

	Возврат Новый(
		ПрилепляемыеКоллекции
			.Получить(Имя)
			.ИначеВызватьИсключение("Неизвестная прилепляемая коллекция " + Имя)
	);

КонецФункции

Функция ЕстьПрилепляемаяКоллекция(Имя) Экспорт
	Возврат ПрилепляемыеКоллекции.Получить(Имя).СодержитЗначение();
КонецФункции
